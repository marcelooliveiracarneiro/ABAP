*&---------------------------------------------------------------------*
*& Report ZMCAR_ALV_BANCO
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT zmcar_alv_banco.

CLASS lcl_main DEFINITION.

  PUBLIC SECTION.
    CLASS-METHODS main.

* ZMCA_CLIENTE
* ZMCA_CONTA

    TYPES: BEGIN OF ty_banco,
             cpf        TYPE zmca_cliente-cpf,
             nome       TYPE zmca_cliente-nome,
             tipo_conta TYPE zmca_conta-tipo_conta,
             desc_conta TYPE string,
             saldo      TYPE zmca_conta-saldo,
             moeda      TYPE zmca_conta-moeda,
           END OF ty_banco.


    CLASS-DATA: gt_banco TYPE TABLE OF ty_banco,
                gv_cpf   TYPE zmca_cliente-cpf.

  PRIVATE SECTION.

    CLASS-METHODS: data_get, data_display.

ENDCLASS.

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE TEXT-001.

  SELECT-OPTIONS: r_cpf FOR lcl_main=>gv_cpf.

SELECTION-SCREEN END OF BLOCK b1.

CLASS lcl_main IMPLEMENTATION.

  METHOD main.

    data_get( ).
    data_display( ).

  ENDMETHOD.

  METHOD data_get.

    SELECT
      cliente~cpf,
      cliente~nome,
      conta~tipo_conta,
      conta~saldo,
      conta~moeda,
      ddtext AS desc_conta
      FROM zmca_cliente AS cliente
      INNER JOIN zmca_conta AS conta
      ON cliente~cpf EQ conta~cpf
      INNER JOIN dd07v
      ON dd07v~domname EQ 'ZAGOD_TIPO_CONTA'
      AND dd07v~ddlanguage EQ @sy-langu
      AND domvalue_l = conta~tipo_conta
      INTO CORRESPONDING FIELDS OF TABLE  @gt_banco
      WHERE cliente~cpf IN @r_cpf.


    IF sy-subrc NE 0.

      MESSAGE TEXT-002 TYPE 'E'.
      LEAVE LIST-PROCESSING.

    ENDIF.

  ENDMETHOD.

  METHOD data_display.

    TRY.

        cl_salv_table=>factory(
          IMPORTING
            r_salv_table   = DATA(lo_salv_table)
          CHANGING
            t_table        = gt_banco
        ).

      CATCH cx_salv_msg.

    ENDTRY.

    IF lo_salv_table IS BOUND.

      lo_salv_table->get_functions( )->set_all( ).
      lo_salv_table->display( ).

    ENDIF.

  ENDMETHOD.

ENDCLASS.

START-OF-SELECTION.

  lcl_main=>main( ).