*&---------------------------------------------------------------------*
*& Report ZMCAR_ALV_BANCO_REV1
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT zmcar_alv_banco_rev1.

CLASS lcl_main DEFINITION.

  PUBLIC SECTION.
    CLASS-METHODS main.

* ZMCA_CLIENTE
* ZMCA_CONTA

    TYPES: BEGIN OF ty_banco,
             cpf        TYPE zmca_cliente-cpf,
             nome       TYPE zmca_cliente-nome,
             endereco   TYPE zmca_cliente-endereco,
             tipo_conta TYPE zmca_conta-tipo_conta,
             desc_conta TYPE string,
             saldo      TYPE zmca_conta-saldo,
             moeda      TYPE zmca_conta-moeda,
           END OF ty_banco.


    CLASS-DATA: gt_banco      TYPE TABLE OF ty_banco,
                gv_cpf        TYPE zmca_cliente-cpf,
                gv_tipo_conta TYPE zmca_conta-tipo_conta.

  PRIVATE SECTION.

    CLASS-METHODS: data_get, data_display.

ENDCLASS.

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE TEXT-001.

  SELECT-OPTIONS: r_cpf FOR lcl_main=>gv_cpf,
                  r_tipo FOR lcl_main=>gv_tipo_conta.

SELECTION-SCREEN END OF BLOCK b1.

CLASS lcl_main IMPLEMENTATION.

  METHOD main.

    data_get( ).
    data_display( ).

  ENDMETHOD.

  METHOD data_get.

    SELECT
      cliente~cpf,
      cliente~nome,
      cliente~endereco,
      conta~tipo_conta,
      conta~saldo,
      conta~moeda
      FROM zmca_cliente AS cliente
      INNER JOIN zmca_conta AS conta
      ON cliente~cpf EQ conta~cpf
      INTO CORRESPONDING FIELDS OF TABLE @gt_banco
      WHERE cliente~cpf IN @r_cpf.

    IF sy-subrc EQ 0.

      SELECT domvalue_l, ddlanguage, ddtext FROM dd07v
        INTO TABLE @DATA(lt_dd07v)
        WHERE domname EQ 'ZAGOD_TIPO_CONTA'
        AND ddlanguage EQ @sy-langu.

      SORT lt_dd07v BY domvalue_l.

      IF sy-subrc EQ 0.

        LOOP AT gt_banco ASSIGNING FIELD-SYMBOL(<fs_banco>).

          READ TABLE lt_dd07v INTO DATA(ls_dd07v) BINARY SEARCH WITH KEY domvalue_l = <fs_banco>-tipo_conta.
          IF sy-subrc EQ 0.
            <fs_banco>-desc_conta = ls_dd07v-ddtext.
          ENDIF.

        ENDLOOP.

      ENDIF.

    ELSE.

      MESSAGE TEXT-002 TYPE 'E'.
      LEAVE LIST-PROCESSING.

    ENDIF.

  ENDMETHOD.

  METHOD data_display.

    TRY.

        cl_salv_table=>factory(
          IMPORTING
            r_salv_table   = DATA(lo_salv_table)
          CHANGING
            t_table        = gt_banco
        ).

      CATCH cx_salv_msg.

    ENDTRY.

    IF lo_salv_table IS BOUND.

      lo_salv_table->get_functions( )->set_all( ).
      lo_salv_table->get_columns( )->set_optimize( abap_true ).
      lo_salv_table->display( ).

    ENDIF.

  ENDMETHOD.

ENDCLASS.

START-OF-SELECTION.

  lcl_main=>main( ).